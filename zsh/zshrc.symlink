ulimit -n 4096

# Uncomment to enable profiling, then view results with `zprof`
# zmodload zsh/zprof

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Development

# Before we do any fancy globbing, be sure extendedglob is enabled
setopt extendedglob

HOMEBREW_PREFIX=$(brew --prefix)

# `brew install zsh-syntax-highlighting`
# https://github.com/zsh-users/zsh-syntax-highlighting
if [ -f ${HOMEBREW_PREFIX}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
  source ${HOMEBREW_PREFIX}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
typeset -gA ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[globbing]='fg=cyan'

# `brew install zsh-autosuggestions`
# https://github.com/zsh-users/zsh-autosuggestions
if [ -f ${HOMEBREW_PREFIX}/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
  source ${HOMEBREW_PREFIX}/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# source every .zsh file in this repo
for config_file ($ZSH/**/*.zsh~*/backups/*) source $config_file

# initialize autocomplete here, otherwise functions won't be loaded
# On slow systems, checking the cached .zcompdump file to see if it must be 
# regenerated adds a noticable delay to zsh startup.  This little hack restricts 
# it to once a day.  It should be pasted into your own completion file.
#
# The globbing is a little complicated here:
# - '#q' is an explicit glob qualifier that makes globbing work within zsh's [[ ]] construct.
# - 'N' makes the glob pattern evaluate to nothing when it doesn't match (rather than throw a globbing error)
# - '.' matches "regular files"
# - 'mh+24' matches files (or directories or whatever) that are older than 24 hours.
autoload -Uz compinit 
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
  compinit;
else
  compinit -C;
fi;

# load every completion after autocomplete loads
for config_file ($ZSH/**/completion.sh~*/backups/*) source $config_file

# Z hopping around folders
if [ -f ${HOMEBREW_PREFIX}/etc/profile.d/z.sh ]; then
  source ${HOMEBREW_PREFIX}/etc/profile.d/z.sh
fi

# Auto-time commands that run longer than $REPORTTIME seconds
export TIMEFMT="%U user %S system %P cpu %*E total, running %J"
REPORTTIME=3

eval `/usr/libexec/path_helper -s`

# source every .path file in this rep -- path updates should come last
for config_file ($ZSH/**/*.path~*/backups/*) source $config_file

# Add iTerm shell integration
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# Finally, set up stderr redirection through colorize to make it red
#exec 2>>(colorize `tput bold; tput setaf 1` `tput sgr0` > /dev/tty &)

# Fix OpenSSL path order
[ -d /usr/local/opt/curl-openssl/bin ] && export PATH="/usr/local/opt/curl-openssl/bin:$PATH"

# Python Setup
export PYENV_VIRTUALENV_DISABLE_PROMPT=1

# Use pyenv over everything
eval "$(pyenv init -)"
eval "$(pyenv virtualenv-init -)"

# ACME.sh Setup
# . "$HOME/.acme.sh/acme.sh.env"

# Created by `pipx` on 2024-06-24 22:22:06
export PATH="$PATH:$HOME/.local/bin"

# -----------------------------------------------------------------------------------= Local RC =--=

# Use .localrc for SUPER SECRET CRAP that you don't want in your public, versioned repo.
# Always put this at the very end so anything in local acts as an override
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# -------------------------------------------------------------------------------------------------=
